group = 'org.interledger'
description = 'Crypto Conditions (Java)'
version = '0.2.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'checkstyle'
apply plugin: 'jacoco'
apply plugin: 'signing'

sourceCompatibility = 1.8
targetCompatibility = 1.8

/*
 custom checkstyle properties for the build.
 see https://docs.gradle.org/current/dsl/org.gradle.api.plugins.quality.CheckstyleExtension.html
*/
checkstyle {
    configFile file('google_checks.xml')
    toolVersion '7.1'
    showViolations = true
    /*
       by default, checkstyle warnings dont fail the build. but we want exactly that for circle-ci
       pass/fail reporting, so tell the plugin to break the build on the first error
    */
    maxWarnings = 0
}

javadoc {
    failOnError false
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

jar {
    manifest {
        attributes 'Implementation-Title': description, 'Implementation-Version': version
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile 'net.i2p.crypto:eddsa:0.2.0'
    testCompile 'junit:junit:4.12'
    testCompile 'org.bouncycastle:bcprov-jdk16:1.46'
    testCompile 'com.fasterxml.jackson.core:jackson-databind:2.7.0'
    testCompile 'org.mockito:mockito-core:2.7.22'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile 'com.google.guava:guava:21.0'
}

if (project.hasProperty('sign')) {
    signing {
        sign configurations.archives
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from tasks.javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

//signing {
//    required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
//    sign configurations.archives
//}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                authentication(userName: hasProperty('ossrhUsername') ? ossrhUsername : '', password: hasProperty('ossrhPassword') ? ossrhPassword : '')
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                authentication(userName: hasProperty('ossrhUsername') ? ossrhUsername : '', password: hasProperty('ossrhPassword') ? ossrhPassword : '')
            }

            pom.project {
                name 'Crypto Conditions (Java)'
                packaging 'jar'
                description 'Java implementation of the Crypto-Conditions RFC found at https://tools.ietf.org/html/draft-thomas-crypto-conditions-03.'
                url 'http://github.com/interledger/java-crypto-conditions'

                inceptionYear '2016'

                properties {
                    "project.build.sourceEncoding" "UTF-8"
                    "maven.compiler.source" "1.8"
                    "maven.compiler.target" "1.8"
                    "checkstyle.config.location" "google_checks.xml"
                    "checkstyle.consoleOutput" "true"
                }

                scm {
                    connection 'scm:git:git://github.com/interledger/java-crypto-conditions.git'
                    developerConnection 'scm:git:git@github.com:interledger/java-crypto-conditions.git'
                    url 'https://github.com/interledger/crypto-conditions'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        name 'Adrian Hope-Bailie'
                        organizationUrl 'https://github.com/adrianhopebailie'
                    }
                    developer {
                        name 'Enrique Benito'
                        organizationUrl 'https://github.com/earizon'
                    }
                    developer {
                        name 'David Fuelling'
                        organizationUrl 'https://github.com/sappenin'
                    }
                    developer {
                        name 'Manuel Polo'
                        organizationUrl 'https://github.com/mrmx'
                    }
                }

                reporting {
                    plugins {
                        plugin {
                            groupId "org.apache.maven.plugins"
                            artifactId "maven-checkstyle-plugin"
                            version "2.17"
                        }
                        plugin {
                            groupId "org.apache.maven.plugins"
                            artifactId "maven-javadoc-plugin"
                            version "2.10.4"
                        }
                    }
                }
            }
        }
    }
}

task syncTestVectors(type: Sync) {
    from 'test-data/test-vectors'
    into 'src/test/resources/org/interledger/cryptoconditions/test/vectors'
}
tasks.processTestResources.dependsOn(syncTestVectors)

tasks.withType(Checkstyle) {
    // Dont bother checking the test Application or HexDump classes, they will probably be removed.
    exclude 'org/interledger/cryptoconditions/Application*'
    exclude 'org/interledger/cryptoconditions/HexDump*'
}

//Maven compatibility task:
task writePom {
    doLast {
        pom {
            project {
                name 'Crypto Conditions (Java)'
                packaging 'jar'
                description 'Java implementation of the Crypto-Conditions RFC found at https://tools.ietf.org/html/draft-thomas-crypto-conditions-02.'
                url 'http://github.com/interledger/java-crypto-conditions'
                inceptionYear '2016'
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
                properties {
                    "project.build.sourceEncoding" "UTF-8"
                    "maven.compiler.source" "1.8"
                    "maven.compiler.target" "1.8"
                    "checkstyle.config.location" "google_checks.xml"
                    "checkstyle.consoleOutput" "true"
                }
                developers {
                    developer {
                        name 'Adrian Hope-Bailie'
                        organizationUrl 'https://github.com/adrianhopebailie'
                    }
                    developer {
                        name 'Enrique Benito'
                        organizationUrl 'https://github.com/earizon'
                    }
                    developer {
                        name 'David Fuelling'
                        organizationUrl 'https://github.com/sappenin'
                    }
                    developer {
                        name 'Manuel Polo'
                        organizationUrl 'https://github.com/mrmx'
                    }
                }
                scm {
                    connection 'scm:git:git://github.com/interledger/java-crypto-conditions.git'
                    developerConnection 'scm:git:git@github.com:interledger/java-crypto-conditions.git'
                    url 'https://github.com/interledger/crypto-conditions'
                }
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
                reporting {
                    plugins {
                        plugin {
                            groupId "org.apache.maven.plugins"
                            artifactId "maven-checkstyle-plugin"
                            version "2.17"
                        }
                        plugin {
                            groupId "org.apache.maven.plugins"
                            artifactId "maven-javadoc-plugin"
                            version "2.10.4"
                        }
                    }
                }
            }
        }.writeTo("pom.xml")
    }
}